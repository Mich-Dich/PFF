#version 460

layout (local_size_x = 16, local_size_y = 16) in;

layout(set = 0, binding = 0) uniform global_scene_data {
    mat4 view;
    mat4 proj;
    mat4 viewproj;
    vec4 ambient_color;
    vec4 sunlight_direction; // w for sun power
    vec4 sunlight_color;
} scene_data;

layout(set = 1, binding = 0) uniform writeonly image2D outImage;

layout(push_constant) uniform constants {
    mat4    inverse_view;
    vec4    middle_sky_color;
    vec4    horizon_sky_color;
    vec4    sunset_color;
    vec4    night_color;
    vec2    image_size;

    // sun parameters
    float   sun_distance;
    float   sun_radius;
    float   sun_core_falloff;           // [0.8-0.99] smaller = softer core
    float   sun_glow_radius_multiplier; // [1.5-4.0] glow halo size
    float   sun_glow_intensity;         // [0.1-1.0] glow brightness
    float   sun_glow_falloff;           // [1.0-4.0] glow edge hardness

    float   FOV_y;

    // cloud parameters
    float   cloud_height;
    float   time;
    float   cloud_density;
    vec4    cloud_color;
    vec4    cloud_color_night;
    vec2    cloud_speed;
    float   cloud_scale;
    float   cloud_coverage;
    float   cloud_persistence;
    float   cloud_detail;
    uint    cloud_octaves;
} PushConstants;

// Shared variables
shared vec2 shared_sun_uv;
shared bool shared_sun_visible;
shared float shared_sun_radius;
shared vec3 shared_sun_dir;

float rand2D(vec2 co) { return fract(sin(dot(co, vec2(12.9898, 78.233))) * 43758.5453); }

float noise2D(vec2 pos) {

    vec2 i = floor(pos);
    vec2 f = fract(pos);
    float a = rand2D(i);
    float b = rand2D(i + vec2(1.0, 0.0));
    float c = rand2D(i + vec2(0.0, 1.0));
    float d = rand2D(i + vec2(1.0, 1.0));
    vec2 u = f * f * (3.0 - 2.0 * f);
    return mix(a, b, u.x) + (c - a)* u.y * (1.0 - u.x) + (d - b) * u.x * u.y;
}

float fbm2D(vec2 pos, uint octaves, float persistence) {

    if(octaves == 0 || persistence <= 0.0)
        return 0.0;

    float total = 0.0;
    float frequency = 1.0;
    float amplitude = 1.0;
    float maxValue = 0.0;
    uint safeOctaves = min(octaves, 8u);
    for(uint i = 0u; i < safeOctaves; ++i) {
        total += noise2D(pos * frequency) * amplitude;
        maxValue += amplitude;
        amplitude *= persistence;
        frequency *= 2.0;
        if (amplitude < 0.001) break;                       // Early exit if amplitude becomes negligible
    }
    return total / max(maxValue, 0.0001);                   // Prevent division by zero
}


// cloud calculation
float calculateCloudDensity(vec3 cameraPos, vec3 worldDir) {

    float planeHeight = PushConstants.cloud_height;
    if (worldDir.y == 0.0)
        return 0.0;

    float t = (planeHeight - cameraPos.y) / worldDir.y;
    if (t < 0.0)
        return 0.0;

    // Calculate cloud position and sample noise
    vec3 cloudPos = cameraPos + worldDir * t;
    vec2 cloudUV = cloudPos.xz * (1.0 / PushConstants.cloud_scale);
    cloudUV += PushConstants.cloud_speed * PushConstants.time;
    float baseNoise = fbm2D(cloudUV, PushConstants.cloud_octaves, PushConstants.cloud_persistence);
    float detailNoise = fbm2D(cloudUV * PushConstants.cloud_detail, PushConstants.cloud_octaves, PushConstants.cloud_persistence);
    float combinedNoise = baseNoise * detailNoise;
    return smoothstep(PushConstants.cloud_coverage, 1.0, combinedNoise) * PushConstants.cloud_density;
}

void main() {

    ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = ivec2(PushConstants.image_size);
    if (texelCoord.x >= size.x || texelCoord.y >= size.y)
        return;

    // First thread computes sun position
    if (gl_LocalInvocationID.x == 0 && gl_LocalInvocationID.y == 0) {
        vec3 sun_dir = normalize(scene_data.sunlight_direction.xyz);
        shared_sun_dir = sun_dir;
        vec3 center = sun_dir * PushConstants.sun_distance;
        vec4 clipC = scene_data.viewproj * vec4(center, 1.0);
        if (clipC.w > 0.0) {
            vec2 ndcC = clipC.xy / clipC.w;
            shared_sun_uv = ndcC * 0.5 + 0.5;
            shared_sun_visible = true;
            float sun_angular_radius = atan(PushConstants.sun_radius / PushConstants.sun_distance);
            float tanHalfFovY = tan(PushConstants.FOV_y * 0.5);
            shared_sun_radius = sun_angular_radius / tanHalfFovY;
        } else {
            shared_sun_visible = false;
        }
    }

    barrier();


    // --- SUNSET CALCULATIONS ---
    float sunElevation = clamp(shared_sun_dir.y, -0.25, 1.0);
    float sunsetFactor = 1.0 - smoothstep(-0.1, 0.25, sunElevation);
    float nightFactor = smoothstep(0.0, -0.25, sunElevation);

    // --- SKY COLOR GRADIENT ---
    vec2 uv = vec2(texelCoord) / vec2(size);
    vec2 ndc = uv * 2.0 - 1.0;
    float aspectRatio = PushConstants.image_size.x / PushConstants.image_size.y;
    float tanHalfFOV = tan(PushConstants.FOV_y * 0.5);

    vec4 viewDir = vec4(ndc.x * aspectRatio * tanHalfFOV, -ndc.y * tanHalfFOV, -1.35, 0.0);
    viewDir = normalize(viewDir);
    vec3 worldDir = normalize((PushConstants.inverse_view * viewDir).xyz);

    // Base sky colors
    vec4 dayMiddle = PushConstants.middle_sky_color;
    vec4 dayHorizon = PushConstants.horizon_sky_color;
    vec4 sunsetMiddle = mix(PushConstants.sunset_color, dayMiddle, 0.7);
    vec4 sunsetHorizon = mix(PushConstants.sunset_color, dayHorizon, 0.5);
    vec4 nightColor = PushConstants.night_color;

    // Vertical blend
    float verticalBlend = clamp(worldDir.y, 0.0, 1.0);
    vec4 color = mix(mix(dayHorizon, dayMiddle, verticalBlend),
                    mix(sunsetHorizon, sunsetMiddle, verticalBlend),
                    sunsetFactor);

    // Add night influence
    color = mix(color, nightColor, nightFactor);

    // --- ATMOSPHERIC EFFECTS ---
    float atmosphereBrightness = mix(mix(1.0, 0.6, sunsetFactor), 0.3, nightFactor);
    color.rgb *= atmosphereBrightness;

    // Final night darkening
    color.rgb = mix(color.rgb, nightColor.rgb, nightFactor);

    // --- STAR CALCULATION ---
    vec3 starColor = vec3(1.0);
    if (nightFactor > 0.0) {


    }

    // --- CLOUD CALCULATION ---
    vec3 cameraPos = PushConstants.inverse_view[3].xyz;
    float density = calculateCloudDensity(cameraPos, worldDir);
    float cloudAlpha = density * PushConstants.cloud_color.a;
    float verticalFactor = abs(worldDir.y);
    float horizonFalloff = smoothstep(0.0, 0.3, verticalFactor);
    density *= horizonFalloff;
    float transmittance = exp(-density * 4.0);
    float scatter = 1.0 - transmittance;
    vec3 baseCloud = mix(PushConstants.cloud_color.rgb, PushConstants.cloud_color_night.rgb, nightFactor);
    color.rgb = mix(color.rgb, baseCloud, scatter * PushConstants.cloud_color.a);

    // --- SUN CALCULATION ---
    if (shared_sun_visible) {
        vec2 delta = uv - shared_sun_uv;
        delta.y *= float(size.y) / float(size.x);
        float distanceToSun = length(delta);
        float sunRadius = shared_sun_radius;

        // Modified sun parameters
        vec3 sunsetSunColor = mix(scene_data.sunlight_color.rgb, vec3(1.0, 0.5, 0.3), sunsetFactor);
        float sunIntensity = scene_data.sunlight_direction.w * mix(1.0, 0.7, sunsetFactor);

        // Sun core
        float sunMask = 1.0 - smoothstep(sunRadius * PushConstants.sun_core_falloff, sunRadius, distanceToSun);
        vec3 sunColorCore = sunsetSunColor * sunMask * sunIntensity * (1.0 - cloudAlpha);

        // Sun glow
        float glowRadius = sunRadius * PushConstants.sun_glow_radius_multiplier;
        float glow = 1.0 - smoothstep(sunRadius, glowRadius, distanceToSun);
        float glowIntensity = PushConstants.sun_glow_intensity * (1.0 + sunsetFactor * 2.0);
        glow = pow(glow, PushConstants.sun_glow_falloff) * glowIntensity;
        vec3 sunColorGlow = sunsetSunColor * glow * sunIntensity * (1.0 - cloudAlpha);

        // Blend with sky
        color.rgb = mix(color.rgb, sunColorCore, sunMask);
        color.rgb += sunColorGlow;
    }

    imageStore(outImage, texelCoord, color);
}